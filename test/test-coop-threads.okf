#< lib/ans-core.okf
#< lib/ans-core-ext.okf
#< lib/coop-threads.okf

\ A simple test for the cooperative threads package.  
\ It sets up two threads, one running NFIB 25 and the other running
\ FACT 11.  After each completes a calculation, it yields to the
\ next thread.
\
\   $ ./okf test/test-coop-threads.okf
\   FACT 11=39916800
\   NFIB 25=242785
\   FACT 11=39916800
\   NFIB 25=242785
\   FACT 11=39916800
\   NFIB 25=242785
\   FACT 11=39916800
\   ...


DECIMAL

: ALLOT-STACK   HERE 4096 CELLS ALLOT CONSTANT ;
: ALLOT-THREAD  HERE THREAD-SIZE ALLOT CONSTANT ;

ALLOT-STACK PARAMETER-STACK-A
ALLOT-STACK RETURN-STACK-A
ALLOT-STACK PARAMETER-STACK-B
ALLOT-STACK RETURN-STACK-B
ALLOT-THREAD THREAD-A
ALLOT-THREAD THREAD-B

: NFIB  DUP 2 < IF DROP 1 ELSE DUP 1 - RECURSE SWAP 2 - RECURSE + 1 + THEN ;

: NFIBBER BEGIN ." NFIB 25=" 25 NFIB . CR THREAD-YIELD AGAIN ;

: NFACT  DUP 2 < IF DROP 1 ELSE DUP 1 - RECURSE * THEN ;

: FACTER BEGIN ." FACT 11=" 11 NFACT . CR THREAD-YIELD AGAIN ;

RETURN-STACK-A PARAMETER-STACK-A ' NFIBBER THREAD-A THREAD-OPEN

RETURN-STACK-B PARAMETER-STACK-B ' FACTER THREAD-B THREAD-OPEN

THREAD-START
