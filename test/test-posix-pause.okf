#< lib/ans-core.okf
#< lib/ans-core-ext.okf
#< lib/posix.okf

\ Test out POSIX-PAUSE.
\ Does so by just calling it after setting up a handler for SIGINT.
\ If you run this inside Shell Mode in GNU Emacs then an easy way to
\ send SIGINT is just to type C-c C-c.
\
\ Run it by doing :-
\
\  $ ./okf test/test-posix-pause.okf
\
\ and if all is well you should see :-
\
\   pausing ...
\
\ If you now send a SIGINT (C-c C-c from inside Emacs) then you should
\ see :-
\
\   caught signal 2
\   pausing over, bye 
\


\ ( -- n )
\ The argument to a signal handler comes through on %esp, so 
\ the following copies it from there to the parameter stack.
\ See bib.linux.sig.handler.

#: SIG#  0824448B , 83FC4589 , 00C304ED ,

#!   8b 44 24 08     movl okf_cell_size(%esp), %eax
#!   89 45 FC        movl %eax, -okf_cell_size(%ebp)
#!   83 ED 04        subl $okf_cell_size, %ebp
#!   C3              ret


\ The simple handler.  All it does is print out a message and then
\ return.  Note that ." and CR use go direct to system calls so it
\ it is safe to use them in a signal handler.

: HANDLER ." caught signal " SIG# . CR ;

CREATE ACTION POSIX-SIGACTION-SIZE ALLOT

' HANDLER ACTION POSIX-SIGACTION-SA-HANDLER + !
0         ACTION POSIX-SIGACTION-SA-MASK    + !
0         ACTION POSIX-SIGACTION-SA-FLAGS   + !

: INSTALL 0 ACTION POSIX-SIGINT POSIX-SIGACTION
            ABORT" could not install SIGINT handler" ;

: BEFORE ." pausing ..." CR ;
: AFTER ." pausing over, bye " CR ;

INSTALL
BEFORE
POSIX-PAUSE
AFTER
